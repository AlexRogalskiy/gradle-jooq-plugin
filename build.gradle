buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        // of plugin dev plugin
        classpath "nu.studer:gradle-plugindev-plugin:$plugindevPluginVersion"
    }
}

apply plugin: 'groovy'
apply plugin: 'nu.studer.plugindev'

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "commons-lang:commons-lang:$comonsLangVersion"

    compile("org.jooq:jooq:$jooqVersion")
    compile("org.jooq:jooq-meta:$jooqVersion")
    compile("org.jooq:jooq-codegen:$jooqVersion")
}

group = 'nu.studer'
version = '1.0.6'

def pomConfig = {
    name project.plugindev.pluginTitle
    description 'Gradle plugin that integrates jOOQ. Adds a task for each source set to generate the sources from a given database schema. The tasks fully participate in the Gradle uptodate checks.'
    url project.plugindev.projectUrl
    inceptionYear '2014'
    developers {
        developer {
            id 'etiennestuder'
            name project.plugindev.authorName
            email 'etienne@studer.nu'
        }
    }
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.html'
        }
    }
}

plugindev {
    pluginTitle = 'Gradle jOOQ plugin'
    authorName = 'Etienne Studer'
    projectUrl = 'https://github.com/etiennestuder/gradle-jooq-plugin'
    done()
}

publishing.publications['mavenJava'].pom.withXml {
    asNode().children().last() + pomConfig
}

bintray {
    user = "$BINTRAY_USER"
    key = "$BINTRAY_API_KEY"

    publications = ['mavenJava']

    pkg {
        repo = 'gradle-plugins'
        name = 'nu.studer.jooq'
        desc = 'Gradle plugin that integrates jOOQ'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin', 'jooq']
        publicDownloadNumbers = true
    }

    dryRun = true
    publish = false
}

project.afterEvaluate {
    bintray.publications.each { pub ->
        bintrayUpload.inputs.files { ->
            project.publishing.publications.getByName(pub).publishableFiles
        }
    }
}

wrapper.gradleVersion = '1.12'
