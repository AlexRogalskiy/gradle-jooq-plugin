import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // of bintray plugin
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayPluginVersion"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "commons-lang:commons-lang:$comonsLangVersion"

    compile("org.jooq:jooq:$jooqVersion")
    compile("org.jooq:jooq-meta:$jooqVersion")
    compile("org.jooq:jooq-codegen:$jooqVersion")
}

group = 'nu.studer'
version = '1.0.3'

compileGroovy {
    sourceCompatibility = compatibilityJreVersion
    targetCompatibility = compatibilityJreVersion
}

project.tasks.withType(Jar).all {
    from file('LICENSE')
    manifest {
        attributes 'Implementation-Title': 'Gradle jOOQ plugin',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Etienne Studer',
                'Build-Date': new SimpleDateFormat("yyyy-MM-dd").format(new Date()),
                'Build-JDK': System.getProperty('java.version'),
                'Build-Gradle': gradle.gradleVersion,
                'Project-Url': 'https://github.com/etiennestuder/gradle-jooq-plugin'
    }
}

task sourcesJar(type: Jar) {
    description = 'Assembles a jar archive containing the main sources.'
    from sourceSets.main.allSource
}

task docsJar(type: Jar) {
    description = 'Assembles a jar archive containing the source documentation.'
    into('javadoc') {
        from javadoc
    }
    into('groovydoc') {
        from groovydoc
    }
}

def pomConfig = {
    name 'Gradle jOOQ plugin'
    description 'Gradle plugin that integrates jOOQ. Adds a task for each source set to generate the sources from a given database schema. The tasks fully participate in the Gradle uptodate checks.'
    url 'https://github.com/etiennestuder/gradle-jooq-plugin'
    inceptionYear '2014'
    developers {
        developer {
            id 'etiennestuder'
            name 'Etienne Studer'
            email 'etienne@studer.nu'
        }
    }
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.html'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact docsJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            name 'localFolder'
            url '../repo'
        }
    }
}

bintray {
    user = "$BINTRAY_USER"
    key = "$BINTRAY_API_KEY"

    publications = ['mavenJava']

    pkg {
        repo = 'gradle-plugins'
        name = 'nu.studer.jooq'
        desc = 'Gradle plugin that integrates jOOQ'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin']
        // publicDownloadNumbers = true
    }

    dryRun = true
    // publish = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
    description = 'Generates the Gradle wrapper files.'
}
