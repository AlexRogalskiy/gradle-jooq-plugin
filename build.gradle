plugins {
    id 'com.gradle.build-scan' version '1.16'
    id 'nu.studer.plugindev' version '1.0.12'
    id 'nu.studer.credentials' version '1.0.5'
    id 'groovy'
}

group = 'nu.studer'
version = '3.0.3-DEV'

buildScan {
    licenseAgreementUrl 'https://gradle.com/terms-of-service'
    licenseAgree 'yes'
}

dependencies {
    compile("org.jooq:jooq:3.11.9")
    compile("org.jooq:jooq-meta:3.11.9")
    compile("org.jooq:jooq-codegen:3.11.9")

    testCompile "com.h2database:h2:1.4.193"
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: 'groovy-all'
    }
}

plugindev {
    pluginImplementationClass 'nu.studer.gradle.jooq.JooqPlugin'
    pluginDescription 'Gradle plugin that integrates jOOQ. For each configuration, the plugin adds a task to generate the sources from a given database schema and includes the generated sources in the specified source set. Multiple configurations are supported. The code generation tasks fully participate in the Gradle uptodate checks.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'jooq'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-jooq-plugin'
    projectInceptionYear '2014'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

def testAll = tasks.create('testAll') {
    group = 'Gradle cross-version test'
}

List<String> testedGradleVersions = []
(8..14).each { testedGradleVersions << "2.$it" }
(0..5).each { testedGradleVersions << "3.$it" }
(0..8).each { testedGradleVersions << "4.$it" }
testedGradleVersions << "2.14.1"
testedGradleVersions << "3.2.1"
testedGradleVersions << "3.4.1"
testedGradleVersions << "3.5.1"
testedGradleVersions << "4.2.1"
testedGradleVersions << "4.3.1"
testedGradleVersions << "4.4.1"
testedGradleVersions << "4.5.1"
testedGradleVersions << "4.8.1"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}
