plugins {
    id 'nu.studer.plugindev' version '3.0'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'nu.studer.credentials' version '2.1'
    id 'org.nosphere.gradle.github.actions' version '1.2.0'
    id 'groovy'
}

group = 'nu.studer'
version = '5.2.3-DEV'

dependencies {
    api 'org.jooq:jooq-codegen:3.14.11'

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-core:2.3.0.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.3'
    implementation 'javax.activation:activation:1.1.1'

    testImplementation 'com.h2database:h2:1.4.200'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-2.5'
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform()
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << '7.1'
testedGradleVersions << '7.0.2'
testedGradleVersions << '6.9'
testedGradleVersions << '6.1.1'

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

gradlePlugin {
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.jooq'
            implementationClass = 'nu.studer.gradle.jooq.JooqPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/etiennestuder/gradle-jooq-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-jooq-plugin'
    description = 'Gradle plugin that integrates jOOQ.'
    tags = ['jooq']

    plugins {
        pluginDevPlugin {
            displayName = 'gradle-jooq-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'nu.studer'
        artifactId = 'gradle-jooq-plugin'
    }
}
