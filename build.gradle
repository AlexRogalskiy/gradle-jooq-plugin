buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // of bintray plugin
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayPluginVersion"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'bintray'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"

    compile("org.jooq:jooq:$jooqVersion")
    compile("org.jooq:jooq-meta:$jooqVersion")
    compile("org.jooq:jooq-codegen:$jooqVersion")
}

group = 'nu.studer'
version = '1.0'

project.tasks.withType(Jar).all {
    from file('LICENSE')
    manifest {
        attributes 'Implementation-Title': 'Gradle jOOQ plugin',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Etienne Studer',
                'Implementation-URL': 'https://github.com/etiennestuder/gradle-jooq-plugin',
                'Build-Date': new Date(),
                'Build-JDK': System.getProperty('java.version'),
                'Build-Gradle': gradle.gradleVersion
    }
}

task jarSources(type: Jar) {
    description = 'Assembles a jar archive containing the main sources.'
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jarSources
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../repo'))
            pom.project {
                name 'Gradle jOOQ plugin'
                description 'Gradle plugin that integrates jOOQ. Adds a task for each source set to generate the sources from a given database schema. The tasks fully participate in the Gradle uptodate checks.'
                url 'https://github.com/etiennestuder/gradle-jooq-plugin'
                inceptionYear '2014'

                developers {
                    developer {
                        id 'etiennestuder'
                        name 'Etienne Studer'
                        email 'etienne@studer.nu'
                    }
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                    }
                }
            }
        }
    }
}

bintray {
    user = "$BINTRAY_USER"
    key = "$BINTRAY_API_KEY"

    configurations = ['archives']

    pkg {
        repo = 'gradle-plugins'
        name = 'nu.studer.jooq'
        desc = 'Gradle plugin that integrates jOOQ'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin']
    }

    dryRun = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0-rc-1'
}
